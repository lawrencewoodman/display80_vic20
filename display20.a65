;======================================================================
; Print text using a 20 column screen
;----------------------------------------------------------------------
; Included by display.a65, see that file for more details
;----------------------------------------------------------------------
; Copyright (C) 2021-2022 Lawrence Woodman <lwoodman@vlifesystems.com>
; Licensed under an MIT licence.  Please see LICENCE.md for details.
;======================================================================

; Dimensions for 20 column screen
RNUMCOL20   = 20
RNUMROW20   = 22


            ;--------------------------------
            ; start20
            ; Start 20 character screen
            ;--------------------------------
start20     lda  #MODE20      ; Set which mode we are in
            sta  mode8020     ; /
            lda  #1           ; Set viewable part of screen to first quarter
            jsr  setView20    ; /
            jsr  cfgScr20     ; Configure screen
            jsr  initClr20    ; Init the colour map
            jmp  clrScr20     ; Clear the screen (RTS)


            ;-----------------------------------------------
            ; setView20
            ; Set the view window on the 20 column screen
            ;-----------------------------------------------
            ; ACC - Quarter to display (1-4)
            ;-----------------------------------------------
setView20   .(
            cmp  #1
            beq  q1
            cmp  #2
            beq  q2
            cmp  #3
            beq  q3
            cmp  #4
            beq  q4
            bne  done

q1          lda  #00          ; Set viewable part of screen
            sta  viewcolge    ; |
            lda  #20          ; |
            sta  viewcollt    ; /
            jmp  done
q2          lda  #20          ; Set viewable part of screen
            sta  viewcolge    ; |
            lda  #40          ; |
            sta  viewcollt    ; /
            jmp  done
q3          lda  #40          ; Set viewable part of screen
            sta  viewcolge    ; |
            lda  #60          ; |
            sta  viewcollt    ; /
            jmp  done
q4          lda  #60          ; Set viewable part of screen
            sta  viewcolge    ; |
            lda  #80          ; |
            sta  viewcollt    ; /

done        rts
.)


            ;-----------------------------------------------
            ; cfgScr20
            ; Configure screen dimensions, position and
            ; memory locations
            ;-----------------------------------------------
cfgScr20    lda  #RVTVORIGIN
            sta  VICCR1       ; Set vertical TV origin
            lda  #(128|RNUMCOL20)
            sta  VICCR2       ; Set number of columns displayed
                              ; and screen address
            lda  #(RNUMROW20*2)
            sta  VICCR3       ; Set number of rows displayed
                              ; and character size
            lda  #(240|2)     ; Set screen and character map addresses
                              ; to $1E00 and $8800
            sta  VICCR5
            rts


            ;-----------------------------------------------
            ; initClr20
            ; Init the colour map to all a single colour
            ;-----------------------------------------------
initClr20   .(
            ldy  #00
            lda  #CLBLACK
loop        sta  COLMAPBASE, y
            iny
            bne  loop
            rts
.)


            ;-------------------------------------
            ; clrScr20
            ; Clear the screen 20 column screen
            ;-------------------------------------
clrScr20    .(
            lda  #00
            sta  SCRADDR
            lda  #>SCRBASE
            sta  SCRADDR+1
            ldy  #00
loop        lda  #SPACE
            sta  (SCRADDR),y      ; Set character map location
            inc  SCRADDR          ; Increment SCRADDR
            bne  doneInc          ; |
            inc  SCRADDR+1        ; /
doneInc     lda  SCRADDR+1
            cmp  #>$2000          ; TODO Replace with constant
            bne  loop
            rts
.)


            ;-----------------------------------------
            ; scrollup20
            ; Scroll 20 column screen up two lines
            ; NOTE two lines to match scrollup80
            ;-----------------------------------------
scrollup20  .(
            lda  #<(SCRBASE+(2*RNUMCOL20))  ; Set copy start locations
            sta  SFROM                      ; |
            lda  #>(SCRBASE+(2*RNUMCOL20))  ; |
            sta  SFROM+1                    ; |
            lda  #<SCRBASE                  ; |
            sta  STO                        ; |
            lda  #>SCRBASE                  ; |
            sta  STO+1                      ; /

            ldy  #0
            ldx  #>(RNUMCOL20*(RNUMROW20-2))  ; msb size of move
            beq  partPage
pageLoop    lda  (SFROM),y                    ; Copy a page
            sta  (STO),y                      ; |
            iny                               ; |
            bne  pageLoop                     ; /
            inc  SFROM+1                      ; Next page
            inc  STO+1                        ; /
            dex
            bne  pageLoop
partPage    ldx  #<(RNUMCOL20*(RNUMROW20-2))
            beq  wipe
partLoop    lda  (SFROM),y                   ; Copy remainder
            sta  (STO),y                     ; |
            iny                              ; |
            dex                              ; |
            bne  partLoop                    ; /

            ; Wipe last line
wipe        lda  #<(SCRBASE+(RNUMCOL20*(RNUMROW20-2)))
            sta  STO
            lda  #>(SCRBASE+(RNUMCOL20*(RNUMROW20-2)))
            sta  STO+1
            ldy  #(2*RNUMCOL20)  ; Wipe two lines of characters
            lda  #SPACE          ; Use a space to wipe the lines
wipeLoop    sta  (STO),y
            dey
            cpy  #$FF
            bne  wipeLoop

            lda  #(SNUMROW-2)      ; Set the cursor to the bottom line
            sta  cry

            rts
.)


            ;-----------------------------------------
            ; putchxy20
            ; Print a character to screen at position
            ; specified on 20 column screen
            ;-----------------------------------------
            ; ACC - character to print (ASCII)
            ;   X - Column on simulated screen
            ;   Y - Row on simulated screen
            ;-----------------------------------------
putchxy20   .(
            ; See if in visible quarter of screen
            cpx  viewcolge
            bcs  ge
            jmp  done
ge          cpx  viewcollt
            bcc  inView
            jmp  done

inView      ; The location is in view
            pha               ; Push character to stack
            sec               ; Align x with first column
            txa               ; |
            sbc  viewcolge    ; |
            tax               ; /

            ; Find column
findCol     txa
            sta  SCRADDR      ; Store column to SCRADDR  - lsb
            lda  #$00         ; |
            sta  SCRADDR+1    ; /   msb


            ; Find location of row
findRow     cpy  #00
            beq  addScrAddr
            ; Add number of real columns to address
            clc
            lda  SCRADDR      ; lsb
            adc  #RNUMCOL20
            sta  SCRADDR      ; lsb
            bcc  noCarry
            inc  SCRADDR+1    ; msb
noCarry     dey
            jmp  findRow

            ; Add $1E00 as address of screen
addScrAddr  clc
            lda  SCRADDR+1    ; msb
            adc  #>SCRBASE
            sta  SCRADDR+1    ; msb

storeCh     pla
            jsr  asctocbm
            ldy  #00
            sta  (SCRADDR), y
done        rts
.)


            ;-----------------------------------------
            ; asctocbm
            ; Convert ASCII to PETSCII
            ;-----------------------------------------
            ; ACC - character to convert
            ;-----------------------------------------
asctocbm    .(
            cmp  #33          ; If acc<=32 then acc=space, return
            bcc  blank        ; /
            cmp  #127         ; If acc>=127 then acc=space, return
            bcs  blank        ; /
            cmp  #64          ; If acc>=33 and acc<=63, return
            bcc  done         ; /
            cmp  #64
            beq  is64
            cmp  #91
            beq  is91
            cmp  #92
            beq  is92
            cmp  #93
            beq  is93
            cmp  #94
            beq  is94
            cmp  #95
            beq  is95
            cmp  #96
            beq  is96
            cmp  #123
            beq  is123
            cmp  #124
            beq  is124
            cmp  #125
            beq  is125
            cmp  #126
            beq  is126
            cmp  #97          ; If acc>=97
            bcs  ge97         ; /
            jmp  done         ; If acc>=65 and acc<=90, return

            ; Series of individual characters to convert
is64        lda  #00          ; At symbol
            beq  done
is91        lda  #27          ; Left square bracket
            bne  done
is92        lda  #127         ; Backslash
            bne  done
is93        lda  #29          ; Right square bracket
            bne  done
is94        lda  #30          ; Caret / up arrow (Approx)
            bne  done
is95        lda  #100         ; Underscore
            bne  done
is96        lda  #39          ; Apostrophe
            bne  done
is123       lda  #117         ; Left brace (approx)
            bne  done
is124       lda  #93          ; Vertical bar
            bne  done
is125       lda  #118         ; Right brace (approx)
            bne  done
is126       lda  #64          ; Tilde (approx)
            bne  done
blank       lda  #32          ; Space used as a blank character
            bne  done

ge97        sec               ; If acc>=97 and acc<=122 then acc=acc-96
            sbc  #96          ; /
done        rts
.)

;====================================
; Data
;====================================
viewcolge   .byt 0            ; Viewable window for 20 columns - col >= val
viewcollt   .byt 0            ; Viewable window for 20 columns - col < val
