;======================================================================
; Handle the shadow screen which contains the original
; ASCII text in full
;----------------------------------------------------------------------
; Included by display.a65, see that file for more details
;----------------------------------------------------------------------
; Copyright (C) 2021-2022 Lawrence Woodman <lwoodman@vlifesystems.com>
; Licensed under an MIT licence.  Please see LICENCE.md for details.
;======================================================================

; Dimensions for shadow screen
RNUMCOLSHD  = 80
RNUMROWSHD  = 22

            ;-------------------------------------
            ; clrScrShad
            ; Clear the shadow screen
            ;-------------------------------------
clrScrShad  .(
            lda  #<shadScr8020
            sta  SHADADDR
            lda  #>shadScr8020
            sta  SHADADDR+1
            ldy  #00
loop        lda  #SPACE
            sta  (SHADADDR),y
            inc  SHADADDR               ; Increment SHADADDR
            bne  doneInc                ; |
            inc  SHADADDR+1             ; /
doneInc     lda  SHADADDR+1             ; Test if at end of shadow screen
            cmp  #>shadScr8020+(80*22)  ; |
            bne  loop                   ; |
            lda  SHADADDR               ; |
            cmp  #<shadScr8020+(80*22)  ; |
            bne  loop                   ; /
            rts
.)


            ;------------------------------------------
            ; redraw
            ; Redraw the screen from the shadow screen
            ;------------------------------------------
            ; TODO check if need redraw might be able to just repoint
            ; screen and character set in some situations
redraw      .(
            lda  #1           ; Set as in middle of redraw
            sta  inRedraw     ; /
            lda  crx          ; Push crx and cry to stack
            pha               ; |
            lda  cry          ; |
            pha               ; /
            lda  #(RNUMCOLSHD-1)
            sta  crx
            lda  #00
            sta  cry
            lda  #<shadScr8020
            sta  SHADADDR
            lda  #>shadScr8020
            sta  SHADADDR+1

loop        ldy  crx             ; Copy a line of text to the screen
            lda  (SHADADDR),y    ; |  y here contains column (crx)
            ldx  crx             ; |
            ldy  cry             ; |
            jsr  putchxy         ; |
            dec  crx             ; |
            bpl  loop            ; /

            lda  #(RNUMCOLSHD-1) ; Move crx back to end of line
            sta  crx             ; /

            clc                  ; Increment SHADADDR by RNUMCOLSHD
            lda  SHADADDR        ; |  lsb
            adc  #RNUMCOLSHD     ; |
            sta  SHADADDR        ; |
            bcc  nextLine        ; |
            inc  SHADADDR+1      ; /  msb

nextLine    inc  cry
            lda  cry
            cmp  #RNUMROWSHD
            bne  loop

            pla
            sta  cry
            pla
            sta  crx

            lda  #0           ; Set as not in middle of redraw
            sta  inRedraw     ; /
            rts
.)


            ;-----------------------------------------
            ; putchxyShd
            ; Store a character in the shadow screen
            ;-----------------------------------------
            ; ACC - character to print (ASCII)
            ;   X - Column on simulated screen
            ;   Y - Row on simulated screen
            ;-----------------------------------------
putchxyShd  .(
            ; Add column to shadScr8020 (shadow screen)
            pha               ; Push character to stack
            lda  inRedraw     ; If in redraw then return
            beq  findCol      ; |
            pla               ; |
            rts               ; /

findCol     clc
            txa
            adc  #<shadScr8020  ; lsb
            sta  SHADADDR       ; lsb
            lda  #00
            adc  #>shadScr8020  ; msb
            sta  SHADADDR+1     ; msb

            ; Find location of row
findRow     cpy  #00
            beq  storeCh

            clc               ; Add number of columns to address
            lda  SHADADDR     ; |  lsb
            adc  #RNUMCOLSHD  ; |
            sta  SHADADDR     ; |  lsb
            bcc  noCarry      ; |
            inc  SHADADDR+1   ; /  msb
noCarry     dey
            jmp  findRow

storeCh     pla
            ldy  #00
            sta  (SHADADDR), y
done        rts
.)


            ;-----------------------------------------
            ; scrollupShad
            ; Scroll shadow screen up two lines
            ; NOTE two lines to match scrollup80
            ;-----------------------------------------
scrollupShad .(
            lda  #<(shadScr8020+(2*RNUMCOLSHD))   ; Set copy start locations
            sta  SFROM                            ; |
            lda  #>(shadScr8020+(2*RNUMCOLSHD))   ; |
            sta  SFROM+1                          ; |
            lda  #<shadScr8020                    ; |
            sta  STO                              ; |
            lda  #>shadScr8020                    ; |
            sta  STO+1                            ; /

            ldy  #0
            ldx  #>(RNUMCOLSHD*(RNUMROWSHD-2))  ; Number of pages to move
            beq  partPage
pageLoop    lda  (SFROM),y                    ; Copy a page
            sta  (STO),y                      ; |
            iny                               ; |
            bne  pageLoop                     ; /
            inc  SFROM+1                      ; Next page
            inc  STO+1                        ; /
            dex
            bne  pageLoop
            ; y would always be 0 at this point
partPage    ldx  #<(RNUMCOLSHD*(RNUMROWSHD-2))   ; Remainder of last page
            beq  wipe
partLoop    lda  (SFROM),y                   ; Copy remainder
            sta  (STO),y                     ; |
            iny                              ; |
            dex                              ; |
            bne  partLoop                    ; /

            ; Wipe last line
wipe        lda  #<(shadScr8020+(RNUMCOLSHD*(RNUMROWSHD-2)))  ; lsb
            sta  STO
            lda  #>(shadScr8020+(RNUMCOLSHD*(RNUMROWSHD-2)))  ; msb
            sta  STO+1
            ldy  #(2*RNUMCOLSHD)   ; Wipe two lines of characters
            lda  #SPACE            ; Use a space to wipe the lines
wipeLoop    sta  (STO),y
            dey
            cpy  #$FF
            bne  wipeLoop

            rts
.)
