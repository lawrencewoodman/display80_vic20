;=====================================================================
; Print text using a 2x8 font
;---------------------------------------------------------------------
; Requires 8k+ memory
;---------------------------------------------------------------------
; Copyright (C) 2021 Lawrence Woodman <lwoodman@vlifesystems.com>
; Licensed under an MIT licence.  Please see LICENCE.md for details.
;=====================================================================

; Configuration
SNUMCOL     = 80              ; Number of simulated text columns
                              ; Must be divisible by 4
SNUMROW     = 22              ; Number of simulated text rows
                              ; Must be divisible by 2
RVTVORIGIN  = 20              ; Vertical TV picture origin
NL          = $0A             ; Newline character

; ASCII characters
SPACE       = 32              ; Space character
BS          = 8               ; Backspace character

; Calculated dimensions
RNUMCOL     = SNUMCOL/4       ; Real number of text columns
RNUMROW     = SNUMROW/2       ; Real number of text rows
SLSTCOL     = SNUMCOL-1       ; Last simulated column

; Colour constants
CLBLACK     = 00
CLBLUE      = 06

; VIC Registers
VICCR1      = $9001           ; Vertical TV picture origin
VICCR2      = $9002           ; Number of columns, part of screen address
VICCR3      = $9003           ; Number of rows, raster location, char size
VICCR5      = $9005           ; Screen map and character map address

; Display memory location constants
CHRMAPBASE  = $1000           ; Base address of character map
SCRBASE     = $1E00           ; Base address of screen
COLMAPBASE  = $9600           ; Base address of colour map


; Zero Page Variable Locations
CHRADDR     = $01             ; 16-bit character map address
                              ; used for calculations
CHQUARTER   = $03             ; Quarter of character definition right to left
CHHALFOFF   = $04             ; Half of character definition offset (half*8)
CHDEFADDR   = $09             ; 16-bit character def address
CRX         = $0B             ; Cursor x position on simulated screen
CRY         = $0C             ; Cursor y position on simulated screen
STRADDR     = $0D             ; 16-bit address of a string to print
SFROM       = $0F             ; 16-bit scroll from address
STO         = $12             ; 16-bit scroll to address
CHDEF       = $14             ; 8 bytes defining a character

            ;--------------------------------
            ; v80_init
            ; Init 80 character screen
            ;--------------------------------
v80_init    lda  #00          ; Init cursor to top left of screen
            sta  CRX          ; |
            sta  CRY          ; /
            jsr  cfgScr       ; Configure screen
            jsr  initClr      ; Init the colour map
            jsr  initScr      ; Init the screen
            jsr  clrChMap     ; Clear character map


            ;-----------------------------------------------
            ; cfgScr
            ; Configure screen dimensions, position and
            ; memory locations
            ;-----------------------------------------------
cfgScr      lda  #RVTVORIGIN
            sta  VICCR1       ; Set vertical TV origin
            lda  #(128|RNUMCOL)
            sta  VICCR2       ; Set number of columns displayed
                              ; and screen address
            lda  #((RNUMROW*2)+1)
            sta  VICCR3       ; Set number of rows displayed
                              ; and character size
            lda  #(240|12)    ; Set screen and character map addresses
                              ; to $1E00 and $1000
            sta  VICCR5
            rts


            ;-----------------------------------------------
            ; initClr
            ; Init the colour map to all a single colour
            ;-----------------------------------------------
initClr     .(
            ldy  #00
            lda  #CLBLACK
loop        sta  COLMAPBASE, y
            cmp  #CLBLACK
            beq  toBlue
            lda  #CLBLACK
            jmp  next
toBlue      lda  #CLBLUE
next        iny
            bne  loop
            rts
.)

            ;-----------------------------------------------
            ; initScr
            ; Fill screen map with characters needed
            ;-----------------------------------------------
            ; TODO improve this loop code
initScr     .(
            ldy  #(RNUMCOL*RNUMROW)
loop        tya
            sta  SCRBASE, y     ; Store character in screen map
            dey
            cpy  #$FF
            bne  loop
            rts
.)


            ;-----------------------------------------------
            ; clrChMap
            ; Clear the screen by zeroing the character map
            ;-----------------------------------------------
clrChMap    .(
            lda  #$00
            sta  CHRADDR
            lda  #>CHRMAPBASE
            sta  CHRADDR+1
            ldy  #00
loop        lda  #00
            sta  (CHRADDR),y      ; Set character map location
            inc  CHRADDR          ; Increment CHRADDR
            bne  doneInc          ; |
            inc  CHRADDR+1        ; /
doneInc     lda  CHRADDR+1
            cmp  #>SCRBASE
            bne  loop
            rts
.)


            ;-----------------------------------------
            ; scrollup
            ; Scroll the text up two lines
            ;-----------------------------------------
scrollup    .(
            lda  #<(CHRMAPBASE+16*RNUMCOL)   ; Set copy start locations
            sta  SFROM                       ; |
            lda  #>(CHRMAPBASE+16*RNUMCOL)   ; |
            sta  SFROM+1                     ; |
            lda  #<CHRMAPBASE                ; |
            sta  STO                         ; |
            lda  #>CHRMAPBASE                ; |
            sta  STO+1                       ; /

            ldy  #0
            ldx  #>(16*RNUMCOL*(RNUMROW-1))  ; msb size of move
            beq  partPage
pageLoop    lda  (SFROM),y                   ; Copy a page
            sta  (STO),y                     ; |
            iny                              ; |
            bne  pageLoop                    ; /
            inc  SFROM+1                     ; Next page
            inc  STO+1                       ; /
            dex
            bne  pageLoop
partPage    ldx  #<(16*RNUMCOL*(RNUMROW-1))
            beq  wipe
partLoop    lda  (SFROM),y                   ; Copy remainder
            sta  (STO),y                     ; |
            iny                              ; |
            dex                              ; |
            bne  partLoop                    ; /

            ; Wipe last two lines
wipe        lda  #<(CHRMAPBASE+(16*RNUMCOL*(RNUMROW-1)))
            sta  STO
            lda  #>(CHRMAPBASE+(16*RNUMCOL*(RNUMROW-1)))
            sta  STO+1
            ldx  #2           ; Wipe a page and a bit
            ldy  #$FF         ; Wipe a page of memory containing two lines
            lda  #00          ; Use a space to wipe the lines
wipeLoop    sta  (STO),y
            dey
            cpy  #$FF
            bne  wipeLoop

            ; Setup the remainder of the page to be deleted
            inc  STO+1
            ldy  #(16*RNUMCOL-256)
            dex
            bne  wipeLoop

            rts
.)

            ;------------------------------------------
            ; putch
            ; Print a character to screen at cursor
            ; and move cursor
            ; This will print NL, BS control characters
            ; and ASCII 32-128 although these will be
            ; folded into single case
            ;------------------------------------------
            ; ACC - character to print
            ;------------------------------------------
putch       .(
            cmp  #NL
            beq  newline      ; If newline character
            cmp  #BS          ; If backspace character
            beq  backspace

            cmp  #SPACE       ; If acc<32 or acc>=129 then ignore
            bcc  done         ; |
            cmp  #129         ; |
            bcs  done         ; /

            ; Print the character
            pha
            lda  CRX
            tax
            lda  CRY
            tay
            pla
            jsr  putchxy

            ; Move cursor
            lda  CRX
            cmp  #SLSTCOL
            bne  nextCol

            ; Move cursor to next line
newline     lda  #0
            sta  CRX
            lda  CRY
            cmp  #(SNUMROW-1)
            beq  scroll       ; Scroll up if on last line
            inc  CRY
            bne  done         ; Equivalant to JMP

backspace   ; Move cursor back one character and delete character
            ldx  CRX          ; If at beginning of line
            beq  crup         ; /
            dec  CRX
            jmp  blank
crup        lda  CRY          ; If at top left of screen ignore key
            beq  done         ; /
            lda  #79          ; Move to end of previous line
            sta  CRX
            dec  CRY
blank       lda  #SPACE       ; Blank out the character
            ldx  CRX          ; |
            ldy  CRY          ; |
            jsr  putchxy      ; |
            jmp  done         ; /

scroll      jsr  scrollup
            lda  #(SNUMROW-2)
            sta  CRY
            jmp  done

nextCol     inc  CRX
done        rts
.)

            ;-----------------------------------------
            ; putchxy
            ; Print a character to screen at position
            ; specified
            ; This will print ASCII 32-95
            ;-----------------------------------------
            ; ACC - character to print
            ;   X - Column on simulated screen
            ;   Y - Row on simulated screen
            ;-----------------------------------------
putchxy     .(
            cmp  #95
            bcc  storeCh      ; If <= 95
            and  #$5F         ; Drop bit 5 (upcase)
storeCh     pha               ; Push character to stack
            lda  #$00         ; Zero CHRADDR
            sta  CHRADDR      ; |
            lda  #$00         ; |
            sta  CHRADDR+1    ; /

            ; Find which quarter of a character definition
            ; NOTE This counts the quarters from right to left
            ; TODO tidy this up
            txa
            and  #3           ; Look at right 2 bits
            sta  CHQUARTER
            lda  #3
            sec
            sbc  CHQUARTER
            sta  CHQUARTER

            ; Find which half of a character definition
            ; and store the offset
            tya
            clc
            and  #1
            asl               ; Multiply by 8
            asl               ; |
            asl               ; /
            sta  CHHALFOFF

            ; Find location of simulated column pair
findCol     txa
            lsr               ; Quarter the column
            lsr               ; |
            sta  CHRADDR      ; /   lsb

            ; Find location of row
            tya               ; Half the row
            lsr               ; |
            tay               ; /
findRow     cpy  #00
            beq  mul16
            ; Add number of real columns to address
            clc
            lda  CHRADDR      ; lsb
            adc  #RNUMCOL
            sta  CHRADDR      ; lsb
addDone1    dey
            jmp  findRow


mul16        ; TODO find better name

            ; Multiply by 16 to find location of character
            ; Shift left 4 times to multiply by 16
            asl  CHRADDR       ; lsb
            rol  CHRADDR+1     ; msb
            asl  CHRADDR       ; lsb
            rol  CHRADDR+1     ; msb
            asl  CHRADDR       ; lsb
            rol  CHRADDR+1     ; msb
            asl  CHRADDR       ; lsb
            rol  CHRADDR+1     ; msb

            ; Add $1000 as address of character map
            clc
            lda  CHRADDR+1    ; msb
            adc  #>CHRMAPBASE
            sta  CHRADDR+1    ; msb

            ; Add CHHALFOFF offset to character map address
            ; This locates in the top or bottom half of the character
            ; definition
            clc
            lda  CHRADDR
            adc  CHHALFOFF
            sta  CHRADDR

            ; Copy the character to the screen
copyCh      lda  #00
            sta  CHDEFADDR+1
            pla               ; Pull character off stack
            sec
            sbc  #SPACE       ; Font definitions are from space onwards
            sta  CHDEFADDR    ; Multiply character by 2
            asl  CHDEFADDR    ; |
            rol  CHDEFADDR+1  ; /

            clc
            lda  CHDEFADDR
            adc  #<fontDef     ; lsb
            sta  CHDEFADDR
            lda  CHDEFADDR+1
            adc  #>fontDef     ; msb
            sta  CHDEFADDR+1


            lda  #1           ; Init bitmasks in self-modifying code
            sta  andL+1       ; |
            sta  andR+1       ; |
            sta  realignL-1   ; /
            sta  realignR-1   ; /

            ; Put character definition into 8 bytes using bits 1 and 2
            ldx  #0
getLoop     ldy  #0
            lda  (CHDEFADDR),y
andL        and  #1
            ldy  #1           ; Align the left bit
realignL    dey               ; |
            beq  alignDoneL   ; |
            lsr               ; |
            jmp  realignL     ; /
alignDoneL
            asl
            sta  CHDEF, x
            ldy  #01
            lda  (CHDEFADDR), y
andR        and  #1
            ldy  #1           ; Align the right bit
realignR    dey               ; |
            beq  alignDoneR   ; |
            lsr               ; |
            jmp  realignR     ; /
alignDoneR
            ora  CHDEF, x

            ; Put character in correct quarter
            ldy  CHQUARTER
realignQ    beq  alignDoneQ
            asl
            asl
            dey
            jmp  realignQ
alignDoneQ

            sta  CHDEF, x
            asl  andL+1       ; Self-modify code to move to next bits
            asl  andR+1       ; |
            inc  realignL-1   ; /
            inc  realignR-1   ; /
            inx
            cpx  #8
            bne  getLoop


            ; Copy the character to the character definition.
            ; This gets the existing definition, clears the bits
            ; where this character will go and ORs the remaining
            ; bits with the new character.
            ldy  #7
            lda  CHQUARTER
            cmp  #00
            beq  q0
            cmp  #01
            beq  q1
            cmp  #02
            beq  q2

q3          lda  (CHRADDR),y
            and  #$3F
            ora  CHDEF, y
            sta  (CHRADDR),y
            dey
            bpl  q3
            jmp  done

q2          lda  (CHRADDR),y
            and  #$CF
            ora  CHDEF, y
            sta  (CHRADDR),y
            dey
            bpl  q2
            jmp  done

q1          lda  (CHRADDR),y
            and  #$F3
            ora  CHDEF, y
            sta  (CHRADDR),y
            dey
            bpl  q1
            jmp  done

q0          lda  (CHRADDR),y
            and  #$FC
            ora  CHDEF, y
            sta  (CHRADDR),y
            dey
            bpl  q0

done        rts
.)


            ;----------------------------------------
            ; puts
            ; Print string
            ;----------------------------------------
            ; STRADDR - Location of zero terminated
            ;           string.  This location is
            ;           left at end of string after
            ;           function returns.
            ;----------------------------------------
puts        .(
loop        ldy  #00
            lda  (STRADDR), y
            beq  done
            jsr  putch
            inc  STRADDR      ; Increment STRADDR
            bne  incDone      ; |
            inc  STRADDR+1    ; /
incDone
            jmp  loop
done        rts
.)


#include "fontdef.a65"        ; The font definitions
