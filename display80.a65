;======================================================================
; Print text using a 2x8 font on an 80 column screen
;----------------------------------------------------------------------
; Included by display.a65, see that file for more details
;----------------------------------------------------------------------
; Copyright (C) 2021-2022 Lawrence Woodman <lwoodman@vlifesystems.com>
; Licensed under an MIT licence.  Please see LICENCE.md for details.
;======================================================================


; Calculated dimensions for 80 column screen
RNUMCOL80   = SNUMCOL/4       ; Real number of text columns
RNUMROW80   = SNUMROW/2       ; Real number of text rows
SLSTCOL80   = SNUMCOL-1       ; Last simulated column


            ;--------------------------------
            ; D80_start80
            ; Start 80 character screen
            ;--------------------------------
D80_start80 lda  #MODE80          ; Set which mode we are in
            sta  D80_Mode         ; /
            jsr  D80_CfgScr80     ; Configure screen
            jsr  D80_InitClr80    ; Init the colour map
            jsr  D80_InitScr80    ; Init the screen
            jmp  D80_ClrScr80     ; Clear the screen by clearing character map


            ;-----------------------------------------------
            ; D80_CfgScr80
            ; Configure screen dimensions, position and
            ; memory locations
            ;-----------------------------------------------
D80_CfgScr80
            lda  #RVTVORIGIN
            sta  VICCR1       ; Set vertical TV origin
            lda  #(128|RNUMCOL80)
            sta  VICCR2       ; Set number of columns displayed
                              ; and screen address
            lda  #((RNUMROW80*2)+1)
            sta  VICCR3       ; Set number of rows displayed
                              ; and character size
            lda  #(240|12)    ; Set screen and character map addresses
                              ; to $1E00 and $1000
            sta  VICCR5
            rts


            ;-----------------------------------------------
            ; D80_InitClr80
            ; Init the colour map to alternate black/blue
            ; colours to help make characters more distinct
            ;-----------------------------------------------
D80_InitClr80   .(
            ldy  #00
            lda  #CLBLACK
loop        sta  COLMAPBASE, y
            cmp  #CLBLACK
            beq  toBlue
            lda  #CLBLACK
            jmp  next
toBlue      lda  #CLBLUE
next        iny
            bne  loop
            rts
.)


            ;-----------------------------------------------
            ; D80_InitScr80
            ; Fill screen map with characters needed
            ;-----------------------------------------------
D80_InitScr80   .(
            ldy  #(RNUMCOL80*RNUMROW80)
loop        tya
            sta  SCRBASE, y     ; Store character in screen map
            dey
            cpy  #$FF
            bne  loop
            rts
.)


            ;-----------------------------------------------
            ; D80_ClrScr80
            ; Clear the screen by zeroing the character map
            ;-----------------------------------------------
D80_ClrScr80    .(
            lda  #$00
            sta  CHRADDR
            lda  #>CHRMAPBASE
            sta  CHRADDR+1
            ldy  #00
loop        lda  #00
            sta  (CHRADDR),y      ; Set character map location
            inc  CHRADDR          ; Increment CHRADDR
            bne  doneInc          ; |
            inc  CHRADDR+1        ; /
doneInc     lda  CHRADDR+1
            cmp  #>SCRBASE
            bne  loop
            rts
.)


            ;------------------------------------------
            ; D80_Scrollup80
            ; Scroll 80 column screen up two lines
            ;------------------------------------------
D80_Scrollup80  .(
            lda  #<(CHRMAPBASE+16*RNUMCOL80)  ; Set copy start locations
            sta  SFROM                        ; |
            lda  #>(CHRMAPBASE+16*RNUMCOL80)  ; |
            sta  SFROM+1                      ; |
            lda  #<CHRMAPBASE                 ; |
            sta  STO                          ; |
            lda  #>CHRMAPBASE                 ; |
            sta  STO+1                        ; /

            ldy  #0
            ldx  #>(16*RNUMCOL80*(RNUMROW80-1))  ; msb size of move
            beq  partPage
pageLoop    lda  (SFROM),y                   ; Copy a page
            sta  (STO),y                     ; |
            iny                              ; |
            bne  pageLoop                    ; /
            inc  SFROM+1                     ; Next page
            inc  STO+1                       ; /
            dex
            bne  pageLoop
partPage    ldx  #<(16*RNUMCOL80*(RNUMROW80-1))
            beq  wipe
partLoop    lda  (SFROM),y                   ; Copy remainder
            sta  (STO),y                     ; |
            iny                              ; |
            dex                              ; |
            bne  partLoop                    ; /

            ; Wipe last two lines
wipe        lda  #<(CHRMAPBASE+(16*RNUMCOL80*(RNUMROW80-1)))
            sta  STO
            lda  #>(CHRMAPBASE+(16*RNUMCOL80*(RNUMROW80-1)))
            sta  STO+1
            ldx  #2           ; Wipe a page and a bit
            ldy  #$FF         ; Wipe a page of memory containing two lines
            lda  #00          ; Use a space to wipe the lines
wipeLoop    sta  (STO),y
            dey
            cpy  #$FF
            bne  wipeLoop

            ; Setup the remainder of the page to be deleted
            inc  STO+1
            ldy  #(16*RNUMCOL80-256)
            dex
            bne  wipeLoop

            lda  #(SNUMROW-2)      ; Set the cursor to one line up from bottom
            sta  D80_Cry

            rts
.)


            ;-----------------------------------------
            ; D80_Putchxy80
            ; Print a character to screen at position
            ; specified on 80 column screen
            ; This will print ASCII 32-126
            ;-----------------------------------------
            ; ACC - character to print (ASCII)
            ;   X - Column on simulated screen
            ;   Y - Row on simulated screen
            ;-----------------------------------------
D80_Putchxy80   .(
            cmp  #97
            bcc  storeCh      ; If <= 96
            cmp  #127
            bcs  ge127
            cmp  #123
            bcs  ge123lt127
            and  #$5F         ; Drop bit 5 (upcase)
            jmp  storeCh

ge123lt127  sec
            sbc  #26
            jmp  storeCh
ge127       lda  #32          ; Space
storeCh     sta  D80_Ch       ; Store the character
            lda  #$00         ; Zero CHRADDR
            sta  CHRADDR      ; |
            lda  #$00         ; |
            sta  CHRADDR+1    ; /

            ; Find which quarter of a character definition
            txa
            and  #3           ; Look at right 2 bits
            sta  D80_Chquarter


            ; Find which vertical half of a character definition
            ; and store the offset
            tya
            lsr
            bcs  vhalf2
            lda  #00
            beq  storeOff
vhalf2      lda  #08
storeOff    sta  D80_Chhalfoff

            ; TODO Put this where zero CHRADDR is above
            ; Find location of simulated column pair
findCol     txa
            lsr               ; Quarter the column
            lsr               ; |
            sta  CHRADDR      ; /   lsb

            ; Find location of row
            tya               ; Half the row
            lsr               ; |
            tay               ; /
findRow     cpy  #00
            beq  mul16
            ; Add number of real columns to address
findRowLoop clc
            lda  CHRADDR      ; lsb
            adc  #RNUMCOL80
            sta  CHRADDR      ; lsb
            dey
            bne  findRowLoop


mul16       ; Multiply by 16 to find location of character
            ; Shift left 4 times to multiply by 16
            asl  CHRADDR       ; lsb
            rol  CHRADDR+1     ; msb
            asl  CHRADDR       ; lsb
            rol  CHRADDR+1     ; msb
            asl  CHRADDR       ; lsb
            rol  CHRADDR+1     ; msb
            asl  CHRADDR       ; lsb
            rol  CHRADDR+1     ; msb

            ; Add $1000 as address of character map
            clc
            lda  CHRADDR+1    ; msb
            adc  #>CHRMAPBASE
            sta  CHRADDR+1    ; msb

            ; Add chhalfoff offset to character map address
            ; This locates in the top or bottom half of the character
            ; definition
            clc
            lda  CHRADDR
            adc  D80_Chhalfoff
            sta  CHRADDR

            ; Copy the character to the screen
            lda  #00
            sta  CHDEFADDR+1
            lda  D80_Ch       ; Load the character
            sec
            sbc  #SPACE       ; Font definitions are from space onwards
            lsr               ; Divide by 4
            lsr               ; /
            sta  CHDEFADDR
            asl  CHDEFADDR    ; Multiply character by 8
            rol  CHDEFADDR+1  ; |
            asl  CHDEFADDR    ; |
            rol  CHDEFADDR+1  ; |
            asl  CHDEFADDR    ; |
            rol  CHDEFADDR+1  ; /

            clc
            lda  CHDEFADDR
            adc  #<D80_fontDef    ; lsb
            sta  CHDEFADDR
            lda  CHDEFADDR+1
            adc  #>D80_fontDef    ; msb
            sta  CHDEFADDR+1

            ; Find character def quarter
            lda  D80_Ch
            and  #3
            sta  D80_Chdefq   ; Save quarter for character def

            ; Calculate char definition mask to extract char def
            ldy  #7
            lda  D80_Chdefq
            beq  qd0
            cmp  #01
            beq  qd1
            cmp  #02
            beq  qd2

qd3         lda  #$3
            sta  D80_Chdefmask
            bne  setchMask
qd2         lda  #$C
            sta  D80_Chdefmask
            bne  setchMask
qd1         lda  #$30
            sta  D80_Chdefmask
            bne  setchMask
qd0         lda  #$C0
            sta  D80_Chdefmask

            ; Calculate char mask to wipe before new char copied
setchMask   lda  D80_Chquarter
            beq  q0
            cmp  #01
            beq  q1
            cmp  #02
            beq  q2

q3          lda  #$FC
            sta  D80_Chmask
            bne  calcQDiff
q2          lda  #$F3
            sta  D80_Chmask
            bne  calcQDiff
q1          lda  #$CF
            sta  D80_Chmask
            bne  calcQDiff
q0          lda  #$3F
            sta  D80_Chmask


            ; Calculate the difference between the quarter of the char
            ; definition and where it is needed
calcQDiff   sec
            lda  D80_Chquarter
            sbc  D80_Chdefq
            sta  D80_Chqdiff
            beq  copyCh       ; No shifting needed so just copy the character
            bpl  shiftRight


            ; Get each row of the character definition and shift it to the
            ; left to line up with where it is needed and then copy it to
            ; redefine the character on the screen
shiftLeft   ldy  #7               ; Number of rows of char definition less one
slLoopA     ldx  D80_Chqdiff
            lda  (CHDEFADDR),y    ; Extract character definition
            and  D80_Chdefmask    ; /
slLoopB     asl                   ; Shift definition to align with where
            asl                   ; /  needed
            inx                   ; Difference is negative so increment
            bne  slLoopB          ; /  until 0
            sta  CHDEFROW
            lda  (CHRADDR),y
            and  D80_Chmask       ; Wipe bits where new character is to go
            ora  CHDEFROW
            sta  (CHRADDR),y
            dey
            bpl  slLoopA
            bmi  done

            ; Get each row of the character definition and shift it to the
            ; right to line up with where it is needed and then copy it to
            ; redefine the character on the screen
shiftRight  ldy  #7               ; Number of rows of char definition less one
srLoopA     ldx  D80_Chqdiff
            lda  (CHDEFADDR),y    ; Extract character definition
            and  D80_Chdefmask    ; /
srLoopB     lsr                   ; Shift definition to align with where
            lsr                   ; /  needed
            dex
            bne  srLoopB
            sta  CHDEFROW
            lda  (CHRADDR),y
            and  D80_Chmask       ; Wipe bits where new character is to go
            ora  CHDEFROW
            sta  (CHRADDR),y
            dey
            bpl  srLoopA
            bmi  done

            ; Straight copy of each row of the character definition without
            ; shifting to redefine the character on the screen
copyCh      ldy  #7
copyChLoop  lda  (CHDEFADDR),y    ; Extract character definition
            and  D80_Chdefmask    ; /
            sta  CHDEFROW
            lda  (CHRADDR),y
            and  D80_Chmask       ; Wipe bits where new character is to go
            ora  CHDEFROW
            sta  (CHRADDR),y
            dey
            bpl  copyChLoop

done        rts
.)

;====================================
; Data
;====================================
D80_Chquarter .byt 0          ; Quarter of character definition
D80_Chhalfoff .byt 0          ; Half of character definition offset (half*8)
D80_Ch        .byt 0          ; A character
D80_Chdefq    .byt 0          ; The quarter that the char definition is in
D80_Chqdiff   .byt 0          ; The difference between the quarter of the char
                              ; definition and where it is needed
D80_Chmask    .byt 0          ; Bitmask used to wipe bits before copy char
                              ; to screen/character map
D80_Chdefmask .byt 0          ; Bitmask used to extract character definition
